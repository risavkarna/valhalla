plugins {
    id 'org.springframework.boot' version '3.3.2'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'java'
    id 'com.github.node-gradle.node' version '7.0.2'
}

group = 'dev.risav'
version = '0.0.1-SNAPSHOT'

java.sourceCompatibility = '21'

bootRun {
    environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: 'local'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-validation')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    runtimeOnly('org.postgresql:postgresql')
    implementation('io.github.wimdeblauwe:error-handling-spring-boot-starter:4.3.0')
    implementation('org.springdoc:springdoc-openapi-starter-webmvc-ui:2.6.0')
    developmentOnly('org.springframework.boot:spring-boot-devtools')
    developmentOnly('org.springframework.boot:spring-boot-docker-compose')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
}

node {
    download.set(true)
    version.set('20.15.1')
}

task npmRunBuild(type: NpmTask) {
    args = ['run', 'build']
    dependsOn npmInstall

    inputs.files(fileTree('node_modules'))
    inputs.files(fileTree('src/main/webapp'))
    inputs.file('angular.json')
    inputs.file('package.json')
    inputs.file('tailwind.config.js')
    inputs.file('tsconfig.json')
    inputs.file('tsconfig.app.json')
    outputs.dir(layout.buildDirectory.dir('resources/main/static'))
}

processResources {
    dependsOn npmRunBuild
}

tasks.withType(Test) {
    useJUnitPlatform()
}
